PREFIX rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX nm:	<http://nomisma.org/id/>
PREFIX nmo:	<http://nomisma.org/ontology#>
PREFIX skos:	<http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:	<http://www.w3.org/2001/XMLSchema#>

SELECT ?type ?label ?startDate ?endDate ?mint ?mintLabel ?den ?denLabel ?authority ?authorityLabel WHERE {
  BIND (<%URI%> as ?symbol)
  {
    SELECT ?symbol ?side WHERE {
    	{?side nmo:hasControlmark ?symbol}
    	UNION {?children skos:broader+ ?symbol .
              ?side nmo:hasControlmark ?children}
    }
  }
?type nmo:hasObverse|nmo:hasReverse ?side
   MINUS {?type dcterms:isReplacedBy ?replaced}
   ?type a nmo:TypeSeriesItem ;
   skos:prefLabel ?label FILTER(langMatches(lang(?label), "en")) .
   OPTIONAL {?type nmo:hasStartDate ?startDate}
   OPTIONAL {?type nmo:hasEndDate ?endDate}
   OPTIONAL {?type nmo:hasAuthority ?authority .
        ?authority skos:prefLabel ?authorityLabel FILTER(langMatches(lang(?authorityLabel), "en"))
   }
   OPTIONAL { {?type nmo:hasMint ?mint}
        UNION {?type nmo:hasMint/rdf:value ?mint }
        ?mint skos:prefLabel ?mintLabel FILTER(langMatches(lang(?mintLabel), "en"))
   }
   OPTIONAL {?type nmo:hasDenomination ?den . 
   	?den skos:prefLabel ?denLabel FILTER(langMatches(lang(?denLabel), "en"))}
} ORDER BY ?startDate